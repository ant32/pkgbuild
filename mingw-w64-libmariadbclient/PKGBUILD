# Maintainer: Philip A Reimer <antreimer@gmail.com>
pkgname=mingw-w64-libmariadbclient
pkgver=5.5.30
pkgrel=2
pkgdesc="MariaDB client libraries (mingw-w64)"
arch=('any')
url="http:/mariadb.org"
license=('GPL')
makedepends=('mingw-w64-gcc' 'wine')
depends=('mingw-w64-crt')
options=('!strip' '!buildflags')
# This package is downloading the complete source files if it would be more correct or better I
# wouldn't mind putting only the dll files in dropbox and having it only download them.
# If someone finds a way to compile libmariadbclient with mingw-w64 please let me know.
source=("http://ftp.osuosl.org/pub/mariadb/mariadb-${pkgver}/win32-packages/mariadb-${pkgver}-win32.zip"
	"http://ftp.osuosl.org/pub/mariadb/mariadb-${pkgver}/winx64-packages/mariadb-${pkgver}-winx64.zip"
	"http://pub.ist.ac.at/~schloegl/software/reimp-0.50.zip")
#http://lists.gnu.org/archive/html/mingw-cross-env-list/2010-11/msg00101.html
md5sums=('1c128812357d633908c4b52a497de8bc'
	 '14090aeeef8f49ec1cc67f4292074dd0'
	 '7794bf911905c2c5aefe3cc0dc956848')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  
  ln -s mariadb-${pkgver}-win32 i686-w64-mingw32
  ln -s mariadb-${pkgver}-winx64 x86_64-w64-mingw32
  
  for _arch in ${_architectures}; do
    cd "${srcdir}/reimp"
    ${_arch}-gcc -g -I . reimp.c util.c ar.c -o reimp.exe
    cd "${srcdir}/${_arch}/lib"
    wine ${srcdir}/reimp/reimp.exe -d libmysql.lib
    /usr/${_arch}/bin/dlltool -k --input-def libmysql.def --dllname libmysql.dll --output-lib libmysql.a
  done
}

package() {
  for _arch in ${_architectures} ; do
    cd ${srcdir}/${_arch}
    mkdir -p ${pkgdir}/usr/${_arch}/{bin,include,lib}
    cp -R include ${pkgdir}/usr/${_arch}
    install -Dm644 lib/libmysql.dll ${pkgdir}/usr/${_arch}/bin/libmysql.dll
    # I could not get libmysql.a to link correctly to libmysql.dll without it being in the same directory.
    # If someone konws the correct way of doing this please tell me.
    cp ${pkgdir}/usr/${_arch}/bin/libmysql.dll ${pkgdir}/usr/${_arch}/lib/libmysql.dll
    install -Dm644 lib/libmysql.a ${pkgdir}/usr/${_arch}/lib/libmysql.a
    ${_arch}-ranlib ${pkgdir}/usr/${_arch}/lib/libmysql.a
  done
}