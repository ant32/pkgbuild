# Maintainer: ant32 <antreimer@gmail.com>

pkgname=mingw-w64-quazip-qt4
pkgver=0.5.1
pkgrel=3
pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package (mingw-w64)"
url="http://sourceforge.net/projects/quazip/"
arch=('any')
depends=('mingw-w64-crt' 'mingw-w64-qt4')
makedepends=('mingw-w64-gcc')
conflicts=('mingw-w64-quazip-qt5')
source=("http://downloads.sourceforge.net/project/quazip/quazip/$pkgver/quazip-$pkgver.tar.gz")
options=('!strip' '!buildflags' 'staticlibs')
license=('LGPL')
md5sums=('eec6b9b6f19654230dfcd158f29ea9d0')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  cd "$srcdir/quazip-$pkgver/quazip"
  
  for _arch in ${_architectures}; do
    # create shared library
    mkdir -p "$srcdir/${_arch}-shared"
    cd "$srcdir/${_arch}-shared"
    ${_arch}-qmake "$srcdir/quazip-$pkgver/quazip/quazip.pro" \
      PREFIX="$pkgdir/usr/${_arch}" \
      CONFIG+=dll \
      LIBS+=-lz
    make
    # crate static library
    mkdir -p "$srcdir/${_arch}-static"
    cd "$srcdir/${_arch}-static"
    ${_arch}-qmake "$srcdir/quazip-$pkgver/quazip/quazip.pro" \
      PREFIX="$pkgdir/usr/${_arch}" \
      CONFIG+=staticlib \
      LIBS+=-lz
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    mkdir -p "${pkgdir}/usr/${_arch}/"{bin,lib,include}
    
    # install shared
    cd "$srcdir/${_arch}-shared"
    make install
    mv "${pkgdir}/usr/${_arch}/lib/libquazip.a" "${pkgdir}/usr/${_arch}/lib/libquazip.dll.a"
    mv "${pkgdir}/usr/${_arch}/lib/"*.dll "${pkgdir}/usr/${_arch}/bin/"
    
    # install static
    cd "$srcdir/${_arch}-static"
    make install
    
    # strip libraries
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/bin/quazip.dll"
    ${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/libquazip.a"
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/lib/libquazip.dll.a"
  done
}
