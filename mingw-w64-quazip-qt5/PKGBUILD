# Maintainer: ant32 <antreimer@gmail.com>

pkgname=mingw-w64-quazip-qt5
pkgver=0.5.1
pkgrel=1
pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package (mingw-w64)"
url="http://sourceforge.net/projects/quazip/"
arch=('any')
depends=('mingw-w64-crt' 'mingw-w64-qt5-qtbase')
makedepends=('mingw-w64-gcc')
source=("http://downloads.sourceforge.net/project/quazip/quazip/$pkgver/quazip-$pkgver.tar.gz")
options=('!strip' '!buildflags')
license=('LGPL')
md5sums=('eec6b9b6f19654230dfcd158f29ea9d0')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  cd "$srcdir/quazip-$pkgver/quazip"
  
  for _arch in ${_architectures}; do
    mkdir "$srcdir/${_arch}-build"
    cd "$srcdir/${_arch}-build"
    /usr/${_arch}/bin/qmake-qt5 "$srcdir/quazip-$pkgver/quazip/quazip.pro" \
	PREFIX="$pkgdir/usr/${_arch}" \
	CONFIG+=staticlib \
	CONFIG+=dll \
	LIBS+=-lz
    make
  done
  #make staticlib
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/${_arch}-build"
    mkdir -p ${pkgdir}/usr/${_arch}/{bin,lib,include}
    make install
    ${_arch}-strip -x ${pkgdir}/usr/${_arch}/lib/*.dll
    ${_arch}-strip -g ${pkgdir}/usr/${_arch}/lib/libquazip.a
    mv ${pkgdir}/usr/${_arch}/lib/*.dll ${pkgdir}/usr/${_arch}/bin/
    chmod 644 ${pkgdir}/usr/${_arch}/lib/libquazip.a
    ${_arch}-ranlib ${pkgdir}/usr/${_arch}/lib/libquazip.a
    #install -Dm644 ../FindQuaZip.cmake "$pkgdir/usr/${_arch}/share/apps/cmake/modules/FindQuaZip.cmake"
  done
}
