# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-glib2
pkgver=2.37.7
pkgrel=1
arch=(any)
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (mingw-w64)"
depends=(mingw-w64-crt mingw-w64-libiconv mingw-w64-gettext mingw-w64-zlib mingw-w64-libffi)
makedepends=(mingw-w64-gcc python2 gettext glib2 automake)
license=(LGPL)
options=(!strip !buildflags)
url="http://www.gtk.org/"
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz"
        "0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch"
        "glib-send-log-messages-to-correct-stdout-and-stderr.patch"
        "glib-prefer-constructors-over-DllMain.patch")
md5sums=('17a9493e3e2efebc8ab81ec8d4480bfb'
         'c08c41118f5318d10f326c7c30e1a33a'
         '7b065f9bc154bf996523974a698b24de'
         '46969e815f3ab19582ba33667a52048b')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "$srcdir/glib-$pkgver"

  patch -Np1 -i $srcdir/0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
  patch -Np0 -i $srcdir/glib-send-log-messages-to-correct-stdout-and-stderr.patch
  patch -Np1 -i $srcdir/glib-prefer-constructors-over-DllMain.patch

  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  unset LDFLAGS

  for _arch in ${_architectures}; do
    export PKG_CONFIG_LIBDIR="/usr/${_arch}/lib/pkgconfig"
    unset PKG_CONFIG_PATH
    rm -rf build-${_arch}-static
    mkdir build-${_arch}-static && pushd build-${_arch}-static
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build=${CHOST} \
      --disable-shared --enable-static
    make
    popd
    rm -rf build-${_arch}-shared
    mkdir build-${_arch}-shared && pushd build-${_arch}-shared
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build=${CHOST} \
      --disable-static
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/glib-$pkgver/build-${_arch}-shared"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-all ${pkgdir}/usr/${_arch}/bin/*.exe
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/bin/*.dll
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/lib/*.dll.a

    make -C $srcdir/glib-$pkgver/build-${_arch}-static DESTDIR=$pkgdir/static install
    ${_arch}-strip --strip-debug ${pkgdir}/static/usr/${_arch}/lib/*.a
    mv $pkgdir/static/usr/${_arch}/lib/*.a $pkgdir/usr/${_arch}/lib/

    rm -rf $pkgdir/static

    rm -rf $pkgdir/usr/${_arch}/{share/{gtk-doc,man,bash-completion,gdb},lib/charset.alias,etc}
    rm $pkgdir/usr/${_arch}/lib/*.def

    rm -f $pkgdir/usr/${_arch}/bin/gdbus-codegen
    rm -rf $pkgdir/usr/${_arch}/lib/gdbus-2.0
  done
}
