# Maintainer: ant32 <antreimer@gmail.com>

pkgname=qcustomplot-qt4
pkgver=1.1.1
pkgrel=1
pkgdesc="Plotting library for Qt5"
url="http://www.qcustomplot.com"
arch=('x86_64' 'i686')
license=('GPL')
depends=('qt4')
conflicts=('qcustomplot-qt5')
source=("http://www.qcustomplot.com/release/${pkgver}/QCustomPlot-sharedlib.tar.gz"
        "http://www.qcustomplot.com/release/${pkgver}/QCustomPlot-source.tar.gz")
sha256sums=('0b88cd0c0d5f95a3d9b95b00efe4a783d89fe269bd7412954b0228b8548edc0f'
            '4bc7128c6c15734f2c31118789b9b1c4a30da2c50fcc63b069c32e8023901950')

build() {
  cd "${srcdir}"

  # fix path to source files
  sed -i 's|../../qcustomplot|../../qcustomplot-source/qcustomplot|g' \
    "${srcdir}/qcustomplot-sharedlib/sharedlib-compilation/sharedlib-compilation.pro"

  qmake-qt4 "${srcdir}/qcustomplot-sharedlib/sharedlib-compilation/sharedlib-compilation.pro"
  make release
}

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}/usr/"{lib,include}
  cp -a libqcustomplot.so* "$pkgdir/usr/lib"
  cp -a qcustomplot-source/qcustomplot.h "${pkgdir}/usr/include"
}
