# Maintainer: Philip A Reimer <antreimer@gmail.com>
# Contributor: Schala Zeal <schalaalexiazeal@gmail.com>
pkgname=mingw-w64-postgresql-libs
pkgver=9.2.4
pkgrel=4
pkgdesc="Libraries for use with PostgreSQL (mingw-w64)"
arch=('any')
url="http://www.postgresql.org/"
license=('custom:PostgreSQL')
makedepends=('mingw-w64-gcc' 'libxml2')
depends=('mingw-w64-crt' 'mingw-w64-gettext' 'mingw-w64-libxml2' 'mingw-w64-openssl' 'mingw-w64-readline' 'mingw-w64-zlib')
# other needed depends 'mingw-w64-tcl' 'mingw-w64-libxslt'
options=(!libtool !strip !buildflags)
source=("http://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2"
        'postgresql-mingw-errno-v2.patch')
md5sums=('6ee5bb53b97da7c6ad9cb0825d3300dd'
         'b06162e219856ff8ea6055b5338c8b6c')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd ${srcdir}/postgresql-${pkgver}

  # Use upstream patch for Windows error checking
  patch -p1 -i ${srcdir}/postgresql-mingw-errno-v2.patch
}

build() {
  unset LDFLAGS
  
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/build-${_arch}" && pushd "${srcdir}/build-${_arch}"
    ../postgresql-$pkgver/configure \
        --host=${_arch} \
        --prefix=/usr/${_arch} \
        --with-openssl \
        --enable-thread-safety \
        --enable-integer-datetimes \
        --enable-nls \
        --with-ldap \
        --with-libxml # \
        #--with-libxslt \
        #--with-tcl --with-tclconfig=/usr/${_arch}/lib

    # Make DLL definition file visible during each arch build
    ln -s ${srcdir}/postgresql-$pkgver/src/interfaces/libpq/libpqdll.def src/interfaces/libpq/
    ln -s ${srcdir}/postgresql-$pkgver/src/interfaces/ecpg/ecpglib/libecpgdll.def src/interfaces/ecpg/ecpglib/
    ln -s ${srcdir}/postgresql-$pkgver/src/interfaces/ecpg/pgtypeslib/libpgtypesdll.def src/interfaces/ecpg/pgtypeslib/
    ln -s ${srcdir}/postgresql-$pkgver/src/interfaces/ecpg/compatlib/libecpg_compatdll.def src/interfaces/ecpg/compatlib/

    for dir in src/interfaces src/bin/pg_config src/bin/psql; do
      make -C ${dir}
    done
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
  
    cd "${srcdir}/build-${_arch}"
    mkdir -p ${pkgdir}/usr/${_arch}/{bin,include,lib}

    # install libs
    for dir in src/interfaces src/bin/pg_config src/bin/psql; do
      make -C ${dir} DESTDIR="${pkgdir}" install
    done
    
    # Rename the .a files to .dll.a as they're actually import libraries and not static libraries
    for implib in ${pkgdir}/usr/${_arch}/lib/*.a; do
      mv $implib ${implib/.a/.dll.a}
    done
    
    mv "$pkgdir/usr/${_arch}/lib/"*.dll "$pkgdir/usr/${_arch}/bin/"
    ${_arch}-strip -g "$pkgdir/usr/${_arch}/lib/"*.a
    ${_arch}-strip --strip-unneeded "$pkgdir/usr/${_arch}/bin/"*.dll
    rm -r "$pkgdir/usr/${_arch}/share"
    rm "$pkgdir/usr/${_arch}/bin/"*.exe
    
    cd src/include
    mkdir -p "${pkgdir}"/usr/${_arch}/include/{libpq,postgresql/internal/libpq}
    
    # these headers are needed by the public headers of the interfaces
    install -m644 pg_config.h "${pkgdir}/usr/${_arch}/include/"
    install -m644 pg_config_os.h "${pkgdir}/usr/${_arch}/include/"
    cd ${srcdir}/postgresql-$pkgver/src/include
    install -m644 postgres_ext.h "${pkgdir}/usr/${_arch}/include/"
    install -m644 libpq/libpq-fs.h "${pkgdir}/usr/${_arch}/include/libpq/"
    install -m644 pg_config_manual.h "${pkgdir}/usr/${_arch}/include/"
    
    # these headers are needed by the not-so-public headers of the interfaces
    install -m644 c.h "${pkgdir}/usr/${_arch}/include/postgresql/internal/"
    install -m644 port.h "${pkgdir}/usr/${_arch}/include/postgresql/internal/"
    install -m644 postgres_fe.h "${pkgdir}/usr/${_arch}/include/postgresql/internal/"
    install -m644 libpq/pqcomm.h "${pkgdir}/usr/${_arch}/include/postgresql/internal/libpq/"
    
  done
}
