pkgname=mingw-w64-postgresql-libs
pkgver=9.2.4
pkgrel=1
pkgdesc="Libraries for use with PostgreSQL (mingw-w64)"
arch=(any)
url="http://www.postgresql.org"
license=("custom:PostgreSQL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt
mingw-w64-libxml2
mingw-w64-openssl
mingw-w64-readline)
options=(!libtool !strip !buildflags)
source=("http://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2")
md5sums=('6ee5bb53b97da7c6ad9cb0825d3300dd')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
	for _arch in ${_architectures}; do
		unset LDFLAGS
		mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		cp -r "$srcdir/postgresql-$pkgver/"* "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		./configure \
			--prefix=/usr/${_arch} \
			--build=$CHOST \
			--host=${_arch} \
			--enable-nls \
			--disable-rpath \
			--with-openssl \
			--with-libxml
		make
	done
}

package() {
	for _arch in ${_architectures}; do
		cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		make DESTDIR="$pkgdir" install
		mv "$pkgdir/usr/${_arch}/lib/"*.dll "$pkgdir/usr/${_arch}/bin/"
		find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
		find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
		find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
		rm -r "$pkgdir/usr/${_arch}/share"
	done
}