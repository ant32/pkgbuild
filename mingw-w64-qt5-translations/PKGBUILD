# Maintainer: Filip Brcic <brcha@gna.org>
# Contributor: ant32 <antreimer@gmail.com>

_qt_module=qttranslations
pkgname="mingw-w64-qt5-translations"
pkgver=5.1.1
pkgrel=2
arch=('any')
pkgdesc="Qt5 for Windows -- QtTranslations module (mingw-w64)"
depends=('mingw-w64-qt5-base')
makedepends=('mingw-w64-gcc' 'mingw-w64-qt5-tools')
options=('!strip' '!buildflags' 'staticlibs')
replaces=("${pkgname/qt5-/qt5-qt}<=5.1.0")
conflicts=("${pkgname/qt5-/qt5-qt}")
license=('GPL3' 'LGPL')
url="http://qt-project.org/"
_pkgfqn="${_qt_module}-opensource-src-${pkgver}"
source=("http://download.qt-project.org/official_releases/qt/5.1/${pkgver}/submodules/${_pkgfqn}.tar.xz")
md5sums=('5ab0f6c326b9dc0a952cc496df2679aa')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "${srcdir}/${_pkgfqn}"

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}

    ${_arch}-qmake-qt5 ../${_qt_module}.pro
    make

    popd
  done
}

package() {
  cd "${srcdir}/${_pkgfqn}"

  for _arch in ${_architectures}; do
    pushd build-${_arch}

    make INSTALL_ROOT="${pkgdir}" install

    popd
  done
}
