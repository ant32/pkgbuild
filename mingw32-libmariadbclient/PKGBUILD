# Maintainer: Philip A Reimer <antreimer@gmail.com>
pkgname=mingw32-libmariadbclient
pkgver=5.5.30
pkgrel=2
pkgdesc="MariaDB client libraries (mingw32)"
arch=('any')
url="http:/mariadb.org"
license=('GPL')
makedepends=('mingw32-gcc' 'wine')
depends=('mingw32-runtime')
options=('!strip' '!buildflags')
source=("http://ftp.osuosl.org/pub/mariadb/mariadb-${pkgver}/win32-packages/mariadb-${pkgver}-win32.zip"
	"http://pub.ist.ac.at/~schloegl/software/reimp-0.50.zip")
#http://lists.gnu.org/archive/html/mingw-cross-env-list/2010-11/msg00101.html
md5sums=('1c128812357d633908c4b52a497de8bc'
	 '7794bf911905c2c5aefe3cc0dc956848')

build() {
  cd "${srcdir}/reimp"
  unset LDFLAGS
  
  unset WINEARCH
  export WINEPREFIX=${srcdir}/wineprefix
  
  i486-mingw32-gcc -g -I . reimp.c util.c ar.c -o reimp.exe
  cd "${srcdir}/mariadb-${pkgver}-win32/lib"
  wine ${srcdir}/reimp/reimp.exe -d libmysql.lib
  /usr/i486-mingw32/bin/dlltool -k --input-def libmysql.def --dllname libmysql.dll --output-lib libmysql.dll.a
}

package() {
  cd "${srcdir}/mariadb-${pkgver}-win32"
  mkdir -p "${pkgdir}"/usr/i486-mingw32/{bin,include,lib}
  cp -R include "${pkgdir}"/usr/i486-mingw32
  install -Dm644 lib/libmysql.dll "${pkgdir}"/usr/i486-mingw32/bin/libmysql.dll
  install -Dm644 lib/libmysql.dll.a "${pkgdir}"/usr/i486-mingw32/lib/libmysql.dll.a
  i486-mingw32-ranlib ${pkgdir}/usr/i486-mingw32/lib/libmysql.dll.a
  i486-mingw32-strip --strip-unneeded ${pkgdir}/usr/i486-mingw32/bin/*.dll
  i486-mingw32-strip --strip-unneeded ${pkgdir}/usr/i486-mingw32/lib/*.dll.a
}