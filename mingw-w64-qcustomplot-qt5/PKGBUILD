# Maintainer: ant32 <antreimer@gmail.com>

pkgname=mingw-w64-qcustomplot-qt5
pkgver=1.0.1
pkgrel=2
pkgdesc="Plotting library for Qt5 (mingw-w64)"
arch=('any')
url="http://www.qcustomplot.com"
license=('GPL')
makedepends=('mingw-w64-gcc')
depends=('mingw-w64-crt' 'mingw-w64-qt5-base')
options=('!strip' '!buildflags' 'staticlibs')
source=("http://www.qcustomplot.com/release/${pkgver}/QCustomPlot-sharedlib.tar.gz"
        "http://www.qcustomplot.com/release/${pkgver}/QCustomPlot-source.tar.gz")
md5sums=('a2efc29cf0349d3f4f773dd09853c821'
         '294674a786e8ac5c235d25d0bc13f936')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  
  # fix path to source files
  sed -i 's|../../qcustomplot|../qcustomplot|g' "${srcdir}/sharedlib-compilation/sharedlib-compilation.pro"
  
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/build-${_arch}"
    cd "${srcdir}/build-${_arch}"

    ${_arch}-qmake-qt5 "${srcdir}/sharedlib-compilation/sharedlib-compilation.pro"
    make release
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}/release"
    mkdir -p "${pkgdir}/usr/${_arch}/"{bin,lib,include}
    cp qcustomplot1.dll "$pkgdir/usr/${_arch}/bin"
    cp libqcustomplot1.a "${pkgdir}/usr/${_arch}/lib/libqcustomplot1.dll.a"
    cp "${srcdir}/qcustomplot.h" "${pkgdir}/usr/${_arch}/include"
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/lib/"*.a
  done
}
