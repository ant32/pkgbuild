# Maintainer: ant32 <antreimer@gmail.com>
# Contributor: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-dbus
pkgver=1.8.0
pkgrel=1
arch=('any')
pkgdesc="Freedesktop.org message bus system (mingw-w64)"
depends=('mingw-w64-crt' 'mingw-w64-expat')
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config')
options=('!strip' '!buildflags' 'staticlibs')
license=("BSD")
url="http://www.freedesktop.org/wiki/Software/dbus"
source=("http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz")
sha256sums=('769f8c7282b535ccbe610f63a5f14137a5549834b0b0c8a783e90891b8d70b13')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/dbus-${pkgver}"
  sed -i 's,THREAD_LIBS="$THREAD_LIBS -lrt",THREAD_LIBS="$THREAD_LIBS",' configure
}

build() {
  cd "${srcdir}/dbus-${pkgver}"
  unset LDFLAGS
  export CFLAGS="-O2 -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4 -mms-bitfields"
  export CXXFLAGS="$CFLAGS"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --host=${_arch} \
      --target=${_arch} \
      --build=${CHOST} \
      --prefix=/usr/${_arch} \
      --with-xml=expat \
      --disable-systemd \
      --disable-tests \
      --disable-Werror
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/dbus-${pkgver}/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    find "${pkgdir}/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "${pkgdir}/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "${pkgdir}/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -rf "${pkgdir}/usr/${_arch}/"{etc,var,libexec,share}
  done
}
