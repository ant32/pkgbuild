# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-dbus
pkgver=1.6.14
pkgrel=1
arch=('any')
pkgdesc="MinGW Windows port of D-Bus (mingw-w64)"
depends=('mingw-w64-crt' 'mingw-w64-glib2' 'mingw-w64-expat')
makedepends=('mingw-w64-gcc')
options=(!strip !buildflags !libtool)
license=('BSD')
url="http://www.freedesktop.org/wiki/Software/dbus"
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz)
md5sums=('1c22aebdf952e92908800f1ec2aeb0c4')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "${srcdir}/dbus-${pkgver}"

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    unset LDFLAGS
    export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
    export CXXFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
    export PKG_CONFIG_LIBDIR="/usr/${_arch}/lib/pkgconfig:/usr/${_arch}/share/pkgconfig"
    unset PKG_CONFIG_PATH

    ../configure \
      --host=${_arch} \
      --target=${_arch} \
      --build=${CHOST} \
      --prefix=/usr/${_arch} \
      --with-xml=expat \
      --disable-systemd
    sed -i 's/-lrt//g' {,*/,*/*/}Makefile
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/dbus-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-all "$pkgdir"/usr/${_arch}/bin/*.exe
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    mv "${pkgdir}"/usr/${_arch}/lib/*.dll.a "${pkgdir}"/
    ${_arch}-strip --strip-unneeded "${pkgdir}"/*.dll.a
    ${_arch}-strip --strip-debug "$pkgdir"/usr/${_arch}/lib/*.a
    mv "${pkgdir}"/*.dll.a "${pkgdir}"/usr/${_arch}/lib/
    rm -rf "$pkgdir"/usr/${_arch}/share/{doc,man}
    rm -rf "$pkgdir"/usr/${_arch}/{etc/rc.d,var,libexec}
    sed -i 's/-lrt//g' "$pkgdir/usr/${_arch}/lib/pkgconfig/dbus-1.pc"
  done
}
