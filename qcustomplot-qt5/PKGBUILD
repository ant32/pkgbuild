# Maintainer: ant32 <antreimer@gmail.com>

pkgname=qcustomplot-qt5
pkgver=1.0.1
pkgrel=2
pkgdesc="Plotting library for Qt5"
url="http://www.qcustomplot.com"
arch=('x86_64' 'i686')
license=('GPL')
depends=('qt5-base')
source=("http://www.qcustomplot.com/release/${pkgver}/QCustomPlot-sharedlib.tar.gz"
        "http://www.qcustomplot.com/release/${pkgver}/QCustomPlot-source.tar.gz")
md5sums=('a2efc29cf0349d3f4f773dd09853c821'
         '294674a786e8ac5c235d25d0bc13f936')

build() {
  cd "${srcdir}"

  # fix path to source files
  sed -i 's|../../qcustomplot|../qcustomplot|g' "${srcdir}/sharedlib-compilation/sharedlib-compilation.pro"

  qmake-qt5 "${srcdir}/sharedlib-compilation/sharedlib-compilation.pro"
  make release
}

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}/usr/"{lib,include}
  cp -a libqcustomplot.so* "$pkgdir/usr/lib"
  cp -a qcustomplot.h "${pkgdir}/usr/include"
}
