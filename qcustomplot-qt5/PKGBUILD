# Maintainer: ant32 <antreimer@gmail.com>

pkgname=qcustomplot-qt5
pkgver=1.2.1
pkgrel=1
pkgdesc="Plotting library for Qt5"
url="http://www.qcustomplot.com"
arch=('x86_64' 'i686')
license=('GPL')
depends=('qt5-base')
source=("http://www.qcustomplot.com/release/${pkgver}/QCustomPlot-sharedlib.tar.gz"
        "http://www.qcustomplot.com/release/${pkgver}/QCustomPlot-source.tar.gz")
sha256sums=('16b3f59182d3703152589d71da580fbded0a67f88ab7df81b86095422d27685b'
            '9ac31c797ce9116c0b062249028c25a8b04a0cb4ae5cd5dcca52c2000a64bcd2')

build() {
  cd "${srcdir}"

  # fix path to source files
  sed -i 's|../../qcustomplot|../../qcustomplot-source/qcustomplot|g' \
    "${srcdir}/qcustomplot-sharedlib/sharedlib-compilation/sharedlib-compilation.pro"

  qmake-qt5 "${srcdir}/qcustomplot-sharedlib/sharedlib-compilation/sharedlib-compilation.pro"
  make release
}

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}/usr/"{lib,include}
  cp -a libqcustomplot.so* "$pkgdir/usr/lib"
  cp -a qcustomplot-source/qcustomplot.h "${pkgdir}/usr/include"
}
