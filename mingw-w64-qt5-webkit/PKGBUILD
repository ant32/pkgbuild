# Maintainer: ant32 <antreimer@gmail.com>
# Contributor: Filip Brcic <brcha@gna.org>

_qt_module=qtwebkit
pkgname="mingw-w64-qt5-webkit"
pkgver=5.2.1
pkgrel=1
arch=('any')
pkgdesc="Qt5 for Windows -- QtWebkit module (mingw-w64)"
depends=('mingw-w64-qt5-base' 'mingw-w64-qt5-declarative' 'mingw-w64-qt5-sensors'
         'mingw-w64-qt5-location' 'mingw-w64-qt5-multimedia' 'mingw-w64-angleproject'
         'mingw-w64-fontconfig' 'mingw-w64-libpng' 'mingw-w64-libjpeg-turbo' 'mingw-w64-libxslt'
         'mingw-w64-zlib' 'mingw-w64-icu' 'mingw-w64-sqlite3' 'mingw-w64-libwebp')
makedepends=('mingw-w64-gcc' 'python' 'gperf' 'ruby' 'mingw-w64-pkg-config')
options=('!strip' '!buildflags' 'staticlibs')
license=('GPL3' 'LGPL')
url="http://qt-project.org/"
_pkgfqn="${_qt_module}-opensource-src-${pkgver}"
source=("http://download.qt-project.org/official_releases/qt/5.2/${pkgver}/submodules/${_pkgfqn}.tar.xz"
        'qt5-qtwebkit-use-correct-icu-libs.patch'
        'qt5-qtwebkit-enable-pkgconfig-support-for-win32-target.patch'
        'qtwebkit-dont-use-bundled-angle-libraries.patch'
        'qt5-qtwebkit-dont-depend-on-icu.patch'
        'revert-qt4-unicode-removal.patch'
        'qt5-qtwebkit-workaround-build-breakage-after-svn-commit-136242.patch'
        'changeset_150223.diff'
        'qtwebkit-opensource-src-5.0.1-debuginfo.patch'
        'webkit-commit-151422.patch')
sha256sums=('ac089723489ee9146020bd8fc715358249d8cea7f2a2e92a692e2a5595a46b5c'
            'fd5f3df7d3a179e0a42724c26fe97953b799ccecc94c4d070e601fe0fd05afd8'
            'ca71c649401dbda0dd8c10ed3469a2e3247c7e196900fc3f731df43e30b2c146'
            '5d402510c19587408ac084ff0eed27b4e01a4a471cee8582828a16b3e5243119'
            '96b73d208e01574a9fde56d85cfb383d8a27b05190820d870c386c9980c08fb5'
            'd9e3928ded3adf98bc6c57f0c40126be29679d767701bdb5d161cc5d85ce81ee'
            '0a793f42c97f2d5bcc2efad004eada2d3ec51a5cb594064a6d835d50c3fbd1bc'
            '148c7e56189015e8e55d80b2a4523f0df714cd4826a1b3b4046aed842fe62f91'
            '5683ac00153f054075fb722c9388cf25cb6034faa49e7ca5731897917da571b1'
            '770582942672bee8f60194c492ae5633aac90ad985c08f33184d16628e1e29e2')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare()
{
  cd "${srcdir}/${_pkgfqn}"

  # The ICU libraries used for cross-compilation are named exactly the same as their native Linux counterpart
  patch -p0 -i "${srcdir}/qt5-qtwebkit-use-correct-icu-libs.patch"

  # By default the build system assumes that pkg-config isn't used for the win32 target
  # However, we're using it in the Fedora MinGW toolchain so make sure it is used automatically
  patch -p0 -i "${srcdir}/qt5-qtwebkit-enable-pkgconfig-support-for-win32-target.patch"

  # Make sure the bundled copy of the ANGLE library isn't used
  patch -p1 -i "${srcdir}/qtwebkit-dont-use-bundled-angle-libraries.patch"

  # qtwebkit depends on qtbase with ICU support.
  # As ICU support in qtbase also introduces over 20MB of additional dependency
  # bloat (and the qtbase libraries itself are only 13MB) the decision was made
  # to build qtbase without ICU support.
  # Make sure qtwebkit doesn't depend on a qtbase with ICU support any more
  patch -p1 -i "${srcdir}/qt5-qtwebkit-dont-depend-on-icu.patch"

  # Undo the removal of the old Qt4 based unicode support
  # as we want to use it instead of the bloated ICU
  patch -p1 -R -i "${srcdir}/revert-qt4-unicode-removal.patch"

  # WebKit svn commit 136242 implemented a split into QtWebKit and QtWebKitWidgets
  # Due to this change a static library named WebKit1.a is created first.
  # After this a shared library is created named Qt5WebKit.dll which contains
  # the contents of this static library and some other object files.
  # However, various symbols in the static library are expected to be exported
  # in the Qt5WebKit.dll shared library. As static libraries normally don't care
  # about exported symbols (everything is exported after all) the decoration
  # attribute Q_DECL_EXPORT won't be set.
  # This results in undefined references when trying to link the QtWebKitWidgets
  # shared library (which depends on various symbols which aren't exported properly
  # in the Qt5WebKit.dll shared library)
  patch -p0 -i "${srcdir}/qt5-qtwebkit-workaround-build-breakage-after-svn-commit-136242.patch"

  # Fix detection of native tools which started to fail as of QtWebkit 5.1.0
  # Caused by upstream commit 150223, so revert it for now
  patch -p1 -R -i "${srcdir}/changeset_150223.diff"

  # smaller debuginfo s/-g/-g1/ (debian uses -gstabs) to avoid 4gb size limit
  patch -p1 -i "${srcdir}/qtwebkit-opensource-src-5.0.1-debuginfo.patch"

  # Revert commit 151422 to fix a build failure which happens because we're not using ICU
  patch -p1 -R -i "${srcdir}/webkit-commit-151422.patch"

  # Make sure the bundled copy of the ANGLE libraries isn't used
  rm -rf Source/ThirdParty/ANGLE
}

build() {
  cd "${srcdir}/${_pkgfqn}"

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}

    ${_arch}-qmake-qt5 QMAKE_CXXFLAGS+=-Wno-c++0x-compat ../WebKit.pro
    make

    popd
  done
}

package() {
  cd "${srcdir}/${_pkgfqn}"

  for _arch in ${_architectures}; do
    pushd build-${_arch}

    make INSTALL_ROOT="${pkgdir}" install

    # The .dll's are installed in both bindir and libdir
    # One copy of the .dll's is sufficient
    rm -f "${pkgdir}/usr/${_arch}/lib/"*.dll

    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/lib/"*.dll.a

    popd
  done

  # .prl files aren't interesting for us
  find "${pkgdir}" -name "*.prl" -delete
}
