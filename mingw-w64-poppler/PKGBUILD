# Maintainer: ant32 <antreimer@gmail.com>

pkgname=mingw-w64-poppler
pkgver=0.24.4
pkgrel=1
pkgdesc="PDF rendering library based on xpdf 3.0 (mingw-w64)"
arch=(any)
url="http://poppler.freedesktop.org"
license=("GPL")
makedepends=('mingw-w64-gcc'
             'mingw-w64-pkg-config'
             'mingw-w64-glib2'
             'mingw-w64-qt4'
             'mingw-w64-qt5-base')
depends=('mingw-w64-crt'
         'mingw-w64-icu'
         'mingw-w64-libtiff'
         'mingw-w64-openjpeg'
         'mingw-w64-cairo')
optdepends=("mingw-w64-glib2: libpoppler-glib"
            "mingw-w64-qt5-base: libpoppler-qt5"
            "mingw-w64-qt4: libpoppler-qt4")
options=('!strip' '!buildflags' 'staticlibs')
source=("http://poppler.freedesktop.org/poppler-${pkgver}.tar.xz"
        "poppler_configureac-qt5.patch")
sha256sums=('c8a3509a17e6f0bbb07eeb1b58276b485974a8bff3f5eef1bc8e150ccd7c01e2'
            '19955521ac684a23fc9b07ee0b3e812720015e273a2432f62c8be4b7e10ad47f')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/poppler-$pkgver"
  patch -p1 -i "$srcdir/poppler_configureac-qt5.patch"
  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  sed -i "s:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:" configure.ac
}

build() {
  cd "$srcdir/poppler-$pkgver"
  autoreconf -fi
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    # curl is disabled because in my qt5 application I couldn't use the postgresql driver
    "${srcdir}"/poppler-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-xpdf-headers \
      --enable-zlib \
      --enable-poppler-qt4 \
      --enable-poppler-qt5 \
      --enable-poppler-glib \
      --disable-libcurl \
      --disable-gtk-test \
      --disable-utils \
      --disable-gtk-doc-html \
      --with-font-configuration=win32
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}