pkgname=mingw-w64-poppler
pkgver=0.24.3
pkgrel=1
pkgdesc="PDF rendering library based on xpdf 3.0 (mingw-w64)"
arch=(any)
url="http://poppler.freedesktop.org"
license=("GPL")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config mingw-w64-glib2 mingw-w64-qt4)
depends=(mingw-w64-crt
mingw-w64-icu
mingw-w64-libtiff
mingw-w64-curl
mingw-w64-openjpeg
mingw-w64-cairo)
optdepends=("mingw-w64-glib2: libpoppler-glib"
"mingw-w64-qt4: libpoppler-qt4")
options=(!strip !buildflags staticlibs)
source=("http://poppler.freedesktop.org/poppler-${pkgver}.tar.xz"
"poppler_configureac-qt5.patch")
md5sums=('31299f26a998029c85295dbbf4c30beb'
         '6ce60b7290a345da8e8d61dbb6cc63c1')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
	cd "$srcdir/poppler-$pkgver"
	patch -p1 -i "$srcdir/poppler_configureac-qt5.patch"
	sed -i -e '/AC_PATH_XTRA/d' configure.ac
  sed -i "s:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:" configure.ac
  autoreconf -fi
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-xpdf-headers \
      --enable-zlib \
      --enable-libcurl \
      --disable-gtk-test \
      --disable-utils \
      --disable-gtk-doc-html \
      --with-font-configuration=win32
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}