# Maintainer: ant32 <antreimer@gmail.com>

_qt_module=qtsystems
pkgname="mingw-w64-qt5-systems-git"
pkgver=5.0.0.beta1.84.gaa651c7
pkgrel=1
arch=('any')
pkgdesc="Qt5 for Windows -- Qt Service Framework (mingw-w64)"
depends=('mingw-w64-qt5-base' 'mingw-w64-qt5-declarative')
makedepends=('mingw-w64-gcc' 'git')
conflicts=('mingw-w64-qt5-systems')
provides=('mingw-w64-qt5-systems')
options=('!strip' '!buildflags' 'staticlibs')
license=('GPL3' 'LGPL')
url="http://qt-project.org/"
_pkgfqn="${_qt_module}"  # "${_qt_module}-opensource-src-${pkgver}"
source=("git://gitorious.org/qt/${_pkgfqn}.git"
        "qt5-qtsystems-fix-case-sensitive-includes.patch"
        "qt5-qtsystems-link-against-correct-win32-libs.patch")
sha256sums=('SKIP'
            'b570ab64d82832697ae96e81efca5cd59c2c64affdccefef15840540eeac4179'
            '921e5b34b12d8095e329a088565e698d3aceece95751ae61c91a4600b85e2bdb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

pkgver() {
  cd "${srcdir}/${_pkgfqn}"
  git describe | sed -e 's/^v//' | tr - .
}

prepare() {
  cd "${srcdir}/${_pkgfqn}"
  # Don't use case-sensitive file names in #include's
  patch -p1 -i ../qt5-qtsystems-fix-case-sensitive-includes.patch

  # Don't use case-sensitive library names when linking
  patch -p1 -i ../qt5-qtsystems-link-against-correct-win32-libs.patch
}

build() {
  cd "${srcdir}/${_pkgfqn}"

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}

    ${_arch}-qmake-qt5 ../${_qt_module}.pro
    make

    popd
  done
}

package() {
  cd "${srcdir}/${_pkgfqn}"

  for _arch in ${_architectures}; do
    pushd build-${_arch}

    make INSTALL_ROOT="${pkgdir}" install

    # The .dll's are installed in both bindir and libdir
    # One copy of the .dll's is sufficient
    rm -f "${pkgdir}/usr/${_arch}/lib/"*.dll

    ${_arch}-strip --strip-all "${pkgdir}/usr/${_arch}/bin/"*.exe
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/lib/"*.dll.a

    popd
  done

  # .prl files aren't interesting for us
  find "${pkgdir}" -name "*.prl" -delete
}
