# Maintainer: ant32 <antreimer@gmail.com>
# Contributor: Filip Brcic <brcha@gna.org>
pkgname=mingw-w64-gettext
pkgver=0.18.3.2
pkgrel=1
arch=('any')
pkgdesc="GNU internationalization library (mingw-w64)"
depends=('mingw-w64-crt' 'mingw-w64-libiconv' 'mingw-w64-termcap')
makedepends=('mingw-w64-gcc')
options=('!strip' '!buildflags' 'staticlibs')
license=('GPL' 'LGPL')
url="http://www.gnu.org/software/gettext/"
source=("http://ftp.gnu.org/pub/gnu/gettext/gettext-${pkgver}.tar.gz"{,.sig})
sha256sums=('d1a4e452d60eb407ab0305976529a45c18124bd518d976971ac6dc7aa8b4c5d7'
            'SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "${srcdir}/gettext-${pkgver}"

  export CFLAGS="-O2 -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4 -mms-bitfields"
  export CXXFLAGS="-O2 -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  unset LDFLAGS
  
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build=${CHOST} \
      --disable-java \
      --disable-native-java \
      --disable-csharp \
      --enable-static \
      --enable-threads=win32 \
      --without-emacs \
      --disable-openmp
    make
    popd
  done
}

package() {
  cd "${srcdir}/gettext-${pkgver}"

  for _arch in ${_architectures}; do
    pushd build-${_arch}
    make DESTDIR="${pkgdir}" install

    ${_arch}-strip "${pkgdir}/usr/${_arch}/bin/"*.exe
    ${_arch}-strip -x -g "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a

    # Drop some useless tools & files
    rm -rf "${pkgdir}/usr/${_arch}/lib/gettext"
    rm -rf "${pkgdir}/usr/${_arch}/share"
    popd
  done
}
